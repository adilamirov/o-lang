package cup;

import java_cup.runtime.*;


parser code {:
    final String LINE() {
        if (this.cur_token == null || ((ComplexSymbolFactory.ComplexSymbol)this.cur_token).xleft == null) {
            return "unknown";
        }
        return Integer.toString(((ComplexSymbolFactory.ComplexSymbol)this.cur_token).xleft.getLine());
    }
:}

terminal CLASS, EXTENDS, IS, END, VAR, METHOD, THIS, WHILE, LOOP, IF,
         THEN, ELSE, RETURN, LPAREN, RPAREN, LBRACKET, RBRCKET, COMMA,
         DOT, COLON, ASSIGN;
terminal String IDENTIFIER;
terminal String INTEGERLITERAL;
terminal String REALLITERAL;
terminal String BOOLEANLITERAL;

nonterminal Program;
nonterminal ClassDeclaration, MemeberDeclaration, MemeberDeclarations;
nonterminal VariableDeclaration, MethodDeclaration, ConstructorDeclaration, VariableDeclarationInMethod;
nonterminal Expression;
nonterminal Parameters, Body;
nonterminal ParameterDeclaration, ParameterDeclarations;
nonterminal Statement;
nonterminal Assignment;
nonterminal WhileLoop, IfStatement, ReturnStatement;
nonterminal ExpressionBody;
nonterminal Arguments;
nonterminal ArgumentExpression;
nonterminal String Primary;

precedence left LPAREN, RPAREN;
precedence right RETURN;

start with Program;

Program ::=
  Program:program ClassDeclaration:cd   {: RESULT = program + "" + cd; :}
| ClassDeclaration:cd                   {: RESULT = cd + ""; :}
| error                                 {: RESULT = null; System.out.println("Compilation error"); :}
;



MemeberDeclarations ::=
  MemeberDeclaration:member MemeberDeclarations:members {: RESULT = member + "\n" + members; :}
|                                                       {: RESULT = ""; :}
;

MemeberDeclaration ::=
  VariableDeclaration:var           {: RESULT = var; :}
| MethodDeclaration:method          {: RESULT = method; :}
| ConstructorDeclaration:cnstrctr   {: RESULT = cnstrctr; :}
;

VariableDeclaration ::=
  VAR IDENTIFIER:name COLON Expression:type   {:
    String stype = type.toString();
    if (stype.contains("(")) {
        RESULT = "public " + stype.split("[(]")[0] + " " + name + " = new " + type + ";";
    } else {
        RESULT = "public " + stype + " " + name + " = new " + type + "();";
    }
  :}
| VAR error                         {: System.out.println("Error in variable declaration in class"); :}
;

VariableDeclarationInMethod ::=
  VAR IDENTIFIER:name COLON Expression:type   {:
    String stype = type.toString();
    if (stype.contains("(")) {
        RESULT = stype.split("[(]")[0] + " " + name + " = new " + type + ";";
    } else {
        RESULT = stype + " " + name + " = new " + type + "();";
    }
  :}
| VAR error                                     {:
    System.out.println("Error in variable declaration in method around line #" + parser.LINE());
    RESULT = null;
    parser._done_parsing = true;
:}
| VAR IDENTIFIER:name COLON error                    {:
                                                    System.out.println("Error in variable declaration in method around line #" + parser.LINE());
                                                    RESULT = null;
                                                    parser._done_parsing = true;
                                                :}
;

MethodDeclaration ::=
  METHOD IDENTIFIER:name Parameters:args COLON IDENTIFIER:return_type IS Body:body END {:
    RESULT = "public " + return_type + " " + name + args + " { " + body + "}";
  :}
| METHOD IDENTIFIER:name COLON IDENTIFIER:return_type IS Body:body END {:
    RESULT = "public " + return_type + name + " () { " + body + "}";
  :}
| METHOD IDENTIFIER:name Parameters:args IS Body:body END {:
    RESULT = "public void " + name + args + " { " + body + "}";
  :}
| METHOD error {:
    System.out.println("Error in method declaration around line #" + parser.LINE());
    RESULT = null;
    parser._done_parsing = true;
:}
;


ClassDeclaration ::=
  CLASS IDENTIFIER:class_id EXTENDS IDENTIFIER:extends_id IS MemeberDeclarations:members END  {:
  members = members.toString().replaceAll("<CLASSNAME>", class_id);
  RESULT = "public class " + class_id + " extends " + extends_id + " { " + members + "}";
  :}
| CLASS IDENTIFIER:class_id IS MemeberDeclarations:members END  {:
    members = members.toString().replaceAll("<CLASSNAME>", class_id);
    RESULT = "public class " + class_id + " {\n" + members + "}";
:}
| CLASS error               {:System.out.println("Error in class declaration around line #" + parser.LINE());
                                                                                RESULT = null;
                                                                                parser._done_parsing = true; :}
;


ConstructorDeclaration ::=
  THIS Parameters:parameters IS Body:body END   {: RESULT = "public " + "<CLASSNAME>" + parameters + "{\n" + body + "\n}"; :}
| THIS IS Body :body END                        {: RESULT = "public " + "<CLASSNAME>" + "()" + "{\n" + body + "\n}"; :}
| THIS error                                    {: System.out.println("Error in Constructor Declaration around line #" + parser.LINE());
                                                                                                 RESULT = null;
                                                                                                 parser._done_parsing = true; :}
;


Parameters ::=
  LPAREN ParameterDeclaration:param ParameterDeclarations:params RPAREN {: RESULT = "(" + param + params + ")"; :}
;


ParameterDeclarations ::=
  ParameterDeclarations:params COMMA ParameterDeclaration:param     {: RESULT = params + "," + param; :}
|                                                                   {: RESULT = "" ;:}
;


ParameterDeclaration ::=
  IDENTIFIER:name COLON IDENTIFIER:type   {: RESULT = type + " " + name ;:}
| COLON error                             {: System.out.println("Error in parameter declaration  around line #" + parser.LINE());
                                                                                                    RESULT = null;
                                                                                                    parser._done_parsing = true; :}
;


Body ::=
  Body:body VariableDeclarationInMethod:var {: RESULT = body + "\n" + var ;:}
| Body:body Statement:statement     {: RESULT = body + "\n" + statement;:}
|                                   {: RESULT = ""; :}
;

Statement ::=
  Assignment:assignment             {: RESULT = assignment + "\n"; :}
| WhileLoop:whileloop               {: RESULT = whileloop + "\n" ; :}
| IfStatement:ifstatement           {: RESULT = ifstatement + "\n"; :}
| ReturnStatement:returnstatement   {: RESULT = returnstatement + "\n"; :}
| Expression:expression             {: RESULT = expression + ";\n"; :}
;

Assignment ::=
  IDENTIFIER:identifier ASSIGN Expression:expr      {: RESULT = identifier + " = " + expr + ";"; :}
| error ASSIGN                                      {: System.out.println("Error in Assignment around line #" + parser.LINE());
                                                                                             RESULT = null;
                                                                                             parser._done_parsing = true; :}
;

WhileLoop ::=
  WHILE Expression:expr LOOP Body:body END    {: RESULT = "while ( " + expr + " ) {\n" + body + "}"; :}
| WHILE error                                 {: System.out.println("Error in while loop around line #" + parser.LINE());
                                                                                             RESULT = null;
                                                                                             parser._done_parsing = true; :}
 ;

IfStatement ::=
  IF Expression:expr THEN Body:body_then ELSE Body:body_else END  {:
  RESULT = "if ( " + expr + ") { " + body_then + "} else { " + body_else + "}" ;
  :}
| IF Expression:expr THEN Body:body END {:
RESULT = "if ( " + expr + " ) { " + body + "}"  ;:}
| IF error                              {: System.out.println("Error in if statement  around line #" + parser.LINE());
                                                                                         RESULT = null;
                                                                                         parser._done_parsing = true; :}
;

ReturnStatement ::=
  RETURN                    {: RESULT = "return;"  ;:}
| RETURN Expression:expr    {: RESULT = "return" + expr + ";"  ;:}
| RETURN error              {: System.out.println("Error in return statement around line #" + parser.LINE());
                                                                                RESULT = null;
                                                                                parser._done_parsing = true; :}
;

Expression ::=
  Primary:prim ExpressionBody:body      {: RESULT = prim + body; :}
| Primary:prim Arguments:args           {: RESULT = prim + args; :}
;

ExpressionBody ::=
  ExpressionBody:body DOT IDENTIFIER:id Arguments:arg    {: RESULT = body + "." + id + arg;:}
| ExpressionBody:body DOT IDENTIFIER:id                  {: RESULT = body + "." + id ;:}
|                                                        {: RESULT = ""  ;:}
;

Arguments ::=
  LPAREN Expression:exp ArgumentExpression:aexp RPAREN   {: RESULT = "(" + exp + aexp + ")" ;:}
| LPAREN RPAREN                                          {: RESULT = "()"; :}
| LPAREN error RPAREN                                           {: System.out.println("No matching parentheses around line #" + parser.LINE());
                                                            RESULT = null; parser._done_parsing = true; :}
;

ArgumentExpression ::=
  ArgumentExpression:aexp COMMA Expression:exp  {: RESULT = aexp + ", " + exp  ;:}
|                                               {: RESULT = ""  ;:}
;

Primary ::=
  INTEGERLITERAL:integer    {: RESULT = integer ;:}
| REALLITERAL:real          {: RESULT = real  ;:}
| BOOLEANLITERAL:bool       {: RESULT = bool  ;:}
| THIS                      {: RESULT = "this";:}
| IDENTIFIER:id             {: RESULT = id  ;:}
;
